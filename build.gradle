// CCM Modding, Nucleum Omnium
buildscript {
    repositories { mavenCentral() }
    dependencies { classpath 'com.github.abrarsyed.gmcp:GMCP:latest.integration' }
}

apply plugin: "gmcp"

group = 'ccm.nucleum_omnium'
version = "1.0.0.${System.getenv().BUILD_NUMBER}"
archivesBaseName = 'Nucleum-Omnium'

sourceSets {
    main {
        java { srcDir 'common' }
        resources { srcDir 'resources' }
    }
}

minecraft {
    minecraftVersion = '1.6.4'
    accessTransformers += 'resources/ccm_at.cfg'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.lang'
        include '**/*.info'
        expand version: version, mcversion: minecraft.minecraftVersion
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.lang'
        exclude '**/*.info'
    }
}

jar {
    appendix = 'universal'
    manifest {
        attributes 'MCVersion' : project.minecraft.minecraftVersion
        attributes 'Version' : project.version
        attributes 'FMLCorePlugin' : 'ccm.nucleum.omnium.asm.OmniumLoader'
        attributes 'FMLCorePluginContainsFMLMod' : 'true'
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    appendix = 'src'
}

// Doesn't work for some odd reason
task javadocJar(type: Jar, dependsOn: javadoc) {
    appendix = 'javadoc'
    from 'build/docs/javadoc'
}

artifacts {
    archives sourceJar //archives javadocJar
}