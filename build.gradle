// CCM Modding, Nucleum Omnium
buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies { 
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.0-SNAPSHOT' 
    }
}

apply plugin: "forge"

group = 'ccm.nucleum_omnium'
version = "1.0.0.${System.getenv().BUILD_NUMBER}"
archivesBaseName = 'Nucleum-Omnium'

sourceSets {
    main {
        java { srcDir 'common' }
        resources { srcDir 'resources' }
    }
}

minecraft {
    version = "1.6.4-LATEST"
    assetDir = "run/assets"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        expand version: version, mcversion: minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
    }
}

jar {
    appendix = 'universal'
    manifest {
        attributes 'MCVersion' : project.minecraft.version
        attributes 'Version' : project.version
        attributes 'FMLCorePlugin' : 'ccm.nucleum.omnium.asm.OmniumLoader'
        attributes 'FMLCorePluginContainsFMLMod' : 'true'
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    appendix = 'src'
}

// Doesn't work for some odd reason
task javadocJar(type: Jar, dependsOn: javadoc) {
    appendix = 'javadoc'
    from 'build/docs/javadoc'
}

artifacts { 
    archives sourceJar 
    //archives javadocJar
}